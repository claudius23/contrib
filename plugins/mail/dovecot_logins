#!@@PERL@@
# -*- perl -*-

=head1 NAME

dovecot - plugin to monitor the dovecot mail server logins

=head1 APPLICABLE SYSTEMS

Hosts running dovecot

=head1 CONFIGURATION

Configuration parameters for @@CONFDIR@@/dovecot,
if you need to override the defaults below:

 [dovecot]
  env.logdir  - Which logfile to use
  env.logfile - What file to read in logdir
  env.doveadm - Path to doveadm binary

=head2 DEFAULT CONFIGURATION

 [dovecot]
  env.logdir  /var/log
  env.logfile mail.log

=head1 AUTHOR

Claudius Herder

=head1 LICENSE

GPLv2

=head1 MAGIC MARKERS

=begin comment

These magic markers are used by munin-node-configure when installing
munin-node.

=end comment

 #%# family=auto
 #%# capabilities=autoconf

=cut

use strict;
use warnings;

use Munin::Plugin;

my $LOGDIR  = (defined($ENV{'logdir'}) ? $ENV{'logdir'} : '/var/log');
my $LOGFILE = (defined($ENV{'logdir'}) ? $ENV{'logfile'} : 'mail.info');
my $DOVEADM = (defined($ENV{'doveadm'}) ? $ENV{'doveadm'} : 'doveadm');

my $logfile = "$LOGDIR/$LOGFILE";

if ( defined($ARGV[0]) and $ARGV[0] eq "autoconf" )
{
    `which $DOVEADM >/dev/null 2>/dev/null`;
    if (!$?)
    {
        my (undef, $s) = testfile($logfile,"autoconf");
        print "$s\n"
    }
    else
    {
         print "no (could not find dovecot)\n";
    }
    exit 0;
}

my @state = restore_state();

my $pos = shift @state;

my %logins = @state;

if ( $ARGV[0] and $ARGV[0] eq "config" )
{
    print "graph_title dovecot logins\n";
    print "graph_vlabel logins / \${graph_period}\n";
    print "graph_category dovecot\n";
    print "graph_scale no\n";
    print "graph_args --base 1000 -l 0\n";
    print "graph_period minute\n";
    print "graph_total total\n";

    foreach my $i (sort keys %logins)
    {
        print "$i.label $i\n";
        print "$i.type DERIVE\n";
        print "$i.info Number of $i logins\n";
    }
    exit 0;
}

my $startsize = (stat $logfile)[7];
$pos = $startsize unless defined($pos);

if ($startsize < $pos)
{
    # Log rotated
    my $rotlogfile = rotlog($logfile);
    if ( testfile($rotlogfile)) {
        parselogfile ($rotlogfile, $pos, (stat $rotlogfile)[7]);
    }
    $pos = 0;
}

$pos = parselogfile($logfile, $pos, $startsize);

foreach my $i (sort keys %logins)
{
    print "$i.value $logins{$i}\n";
}

save_state($pos, %logins);

sub parselogfile
{
    my ($fname, $start, $stop) = @_;

    my ($logfd, $reset) = tail_open($fname, $start);

    while (tell($logfd) < $stop)
    {
        my $line = <$logfd>;
        chomp ($line);

        if($line =~ /dovecot/)
        {
            if($line =~ /imap-login: Login:/)
            {
                if($line =~ /secured/)
                {
                    $logins{imap_secured}++;
                }
                elsif($line =~ /SSL/)
                {
                    $logins{imap_ssl}++;
                }
                elsif($line =~ /TLS/)
                {
                    $logins{imap_tls}++;
                }
                else
                {
                    $logins{imap}++;
                }
            }
            elsif($line =~ /pop3-login: Login:/)
            {
                if($line =~ /secured/)
                {
                    $logins{pop3_secured}++;
                }
                elsif($line =~ /SSL/)
                {
                    $logins{pop3_ssl}++;
                }
                elsif($line =~ /TLS/)
                {
                    $logins{pop3_tls}++;
                }
                else
                {
                    $logins{pop3}++;
                }
            }
            elsif($line =~ /managesieve-login: Login:/)
            {
                if($line =~ /secured/)
                {
                    $logins{sieve_secured}++;
                }
                elsif($line =~ /SSL/)
                {
                    $logins{sieve_ssl}++;
                }
                elsif($line =~ /TLS/)
                {
                    $logins{sieve_tls}++;
                }
                else
                {
                    $logins{sieve}++;
                }
            }
        }
    }
    return tail_close($logfd);
}

# vim:syntax=perl
